buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
		mavenCentral()
	}
}

plugins {
	id 'com.github.johnrengelman.shadow' version '4.0.4'
	id 'java'
	id 'idea'
	id 'eclipse'
	id 'maven-publish'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '2.3.5'

def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
    version = version + "." + "${System.getenv().BUILD_NUMBER}"
}
// Temp for alexiil's fork
version += "-alexiil.2"

group = 'org.quiltmc'
archivesBaseName = 'quilt-loader-sat4j'

repositories {
	mavenCentral()
}

dependencies {
}

sourceSets {
	main {
		java {
			srcDir 'org.sat4j.core/src/main/java'
			srcDir 'org.sat4j.pb/src/main/java'
			// FABRIC: Exclude some files that we don't need
			exclude 'org/sat4j/pb/LanceurPseudo2005.java'
			exclude 'org/sat4j/pb/LanceurPseudo2007.java'
			exclude 'org/sat4j/pb/LanceurPseudo2007Eclipse.java'
			exclude 'org/sat4j/pb/LPStringSolver.java'
			exclude 'org/sat4j/pb/OPBStringSolver.java'
			exclude 'org/sat4j/pb/PseudoBitsAdderDecorator.java'
			exclude 'org/sat4j/pb/UserFriendlyPBStringSolver.java'
			exclude 'org/sat4j/pb/reader/*'
			exclude 'org/sat4j/pb/tools/SearchOptimizerListener.java'
		}
		resources {
			srcDir 'org.sat4j.core/src/main/resources'
			srcDir 'org.sat4j.pb/src/main/resources'
		}
	}
}

jar {
	classifier = "lib"
}

shadowJar {
	classifier = null
	relocate 'org.sat4j', 'org.quiltmc.loader.util.sat4j'
}

build.dependsOn(shadowJar)

publishing {
       // Temp for alexiil's fork
    repositories {
        maven {
            url System.getenv("MAVEN_DIR") ?: "$projectDir/build/maven"
        }
    }
	publications {
		shadow(MavenPublication) { publication ->
			project.shadow.component(publication)
			groupId 'org.quiltmc'
			artifactId "quilt-loader-sat4j"
			version version
		}
	}
	repositories {
		if (ENV.MAVEN_URL) {
			maven {
				url ENV.MAVEN_URL
				credentials {
					username ENV.MAVEN_USERNAME
					password ENV.MAVEN_PASSWORD
				}
			}
		}
	}
}

artifacts {
	archives shadowJar
}

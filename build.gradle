buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
		mavenCentral()
	}
}

plugins {
	id 'com.github.johnrengelman.shadow' version '4.0.4'
	id 'java'
	id 'idea'
	id 'eclipse'
	id 'maven-publish'
}

def ENV = System.getenv()

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '2.3.5.1'

group = 'org.quiltmc'
archivesBaseName = 'quilt-loader-sat4j'

repositories {
	mavenCentral()
}

dependencies {
}

sourceSets {
	main {
		java {
			srcDir 'org.sat4j.core/src/main/java'
			srcDir 'org.sat4j.pb/src/main/java'
			// QUILT: Exclude some files that we don't need
			exclude 'org/sat4j/pb/LanceurPseudo2005.java'
			exclude 'org/sat4j/pb/LanceurPseudo2007.java'
			exclude 'org/sat4j/pb/LanceurPseudo2007Eclipse.java'
			exclude 'org/sat4j/pb/LPStringSolver.java'
			exclude 'org/sat4j/pb/OPBStringSolver.java'
			exclude 'org/sat4j/pb/PseudoBitsAdderDecorator.java'
			exclude 'org/sat4j/pb/UserFriendlyPBStringSolver.java'
			exclude 'org/sat4j/pb/reader/*'
			exclude 'org/sat4j/pb/tools/SearchOptimizerListener.java'
		}
		resources {
			srcDir 'org.sat4j.core/src/main/resources'
			srcDir 'org.sat4j.pb/src/main/resources'
			// QUILT: We don't need web arifacts
			exclude '**/*.html'
		}
	}
}

jar {
	classifier = "lib"
}

shadowJar {
	classifier = null
	relocate 'org.sat4j', 'org.quiltmc.loader.util.sat4j'
}

build.dependsOn(shadowJar)

publishing {
	publications {
		shadow(MavenPublication) { publication ->
			project.shadow.component(publication)
			groupId 'org.quiltmc'
			artifactId "quilt-loader-sat4j"
			version version
		}
	}
	repositories {
		if (ENV.MAVEN_URL) {
			maven {
				url ENV.MAVEN_URL
				credentials {
					username ENV.MAVEN_USERNAME
					password ENV.MAVEN_PASSWORD
				}
			}
		}
	}
}

artifacts {
	archives shadowJar
}
